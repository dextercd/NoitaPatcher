install(FILES README.txt
    DESTINATION .
    COMPONENT NoitaPatcher_Docs
)

set(CPACK_PACKAGE_NAME "NoitaPatcher")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "${CPACK_PACKAGE_NAME}")

set(CPACK_PACKAGE_VENDOR "dextercd")

set(CPACK_ARCHIVE_COMPONENT_INSTALL ON)
set(CPACK_COMPONENTS_GROUPING ALL_COMPONENTS_IN_ONE)
set(CPACK_INCLUDE_TOPLEVEL_DIRECTORY OFF)
set(CPACK_GENERATOR ZIP)

set(CPACK_PACKAGE_FILE_NAME
    "${PROJECT_NAME}-${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}")

set(CPACK_COMPONENTS_ALL
    NoitaPatcher NoitaPatcher_Docs NSEW_Library
)

list(APPEND CPACK_INSTALL_CMAKE_PROJECTS
    ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} NSEW_Client /Examples
    ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} NSEW_Server /Examples
    ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} NSEW_Example /Examples
    ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} NP_Example /Examples
)

foreach(NP_COMPONENT IN LISTS CPACK_COMPONENTS_ALL)
    list(APPEND CPACK_INSTALL_CMAKE_PROJECTS
        ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} "${NP_COMPONENT}" /
    )
endforeach()

include(FetchContent)
FetchContent_Declare(
    Handtokening_CMake
    GIT_REPOSITORY https://github.com/dextercd/Handtokening-CMake.git
    GIT_TAG b492761e64dffba932a5c51a8123869a1149ed49
)
FetchContent_MakeAvailable(Handtokening_CMake)

list(APPEND HT_SIGN_PATTERNS
    "nsew_server\\.exe" "description=Example one-way world sync server"
    "nsew_native\\.dll" "description=NSEW Native Module"
    "noitapatcher\\.dll" "description=Unofficial Modding API Extension"
)
include(Handtokening)

set(CPACK_VERBATIM_VARIABLES ON)
include(CPack)
